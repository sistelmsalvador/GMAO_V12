#<AdxTL>@(#)0.0.0.0 $Revision$ Centros de trabajo
#####################################################################################
# Src: SPEMWS
#-------------------------------------------------------------------------------------
# Proyecto: Estándar
#-------------------------------------------------------------------------------------
# Version: v12
#-------------------------------------------------------------------------------------
# YAPMP - Creación OF's Mantenimiento preventivo
# Fecha inicio YAPMP: 14/05/2019
# Desarrollador: JCH
# Código de desarrollo: YAPMP
# Descripción del desarrollo: Lanzamiento preventivo de órdenes de fabricación
# Fecha: 14/05/2019
# Codigo Actividad: YAPMP
#######################################################################################


$ACTION

#If GUSER="AIT07" Then : Infbox ACTION : Endif
Case ACTION
  When "OUVRE"        : Gosub OUVRE
  When "LIENS"        : Gosub LIENS
  When "SETBOUT"      : Gosub SETBOUT
  When "MODIF"        : Gosub MODIF
  When "CREATION"     : Gosub CREATION
  When "ANNULE"       : Gosub ANNULE
  When "SEL_TABLE"    : Gosub SEL_TABLE
  When "VERF_TABLE"   : Gosub VERF_TABLE
  When "FIN"          : Gosub FIN
  When Default
Endcase

Return

##############################################################
######################    ACCIONES    ########################
##############################################################
##############################################################
$OUVRE
  Gosub ABRIR_TABLAS
Return

##############################################################
$SETBOUT
# YAPMP
#  Grizo [M:YMP1]YHORASUSO, YTNUSO, YUDUSO, YCTDUSO
  If [M:YMP1]YGESFLG <> 2 Then
    Grizo [M:YMP1]YUDCRTL, YCANTIDAD, YUNIDAD, YBLKLN
  Else
    Actzo [M:YMP1]YUDCRTL, YCANTIDAD, YUNIDAD, YBLKLN
  Endif
# YAPMP
Return

##############################################################
$LIENS
# YAPMP
  Read [F:YMP3]YMP0 = [M:MWS0]WST;[M:MWS0]WCRFCY
  If !fstat Then
    [M:YMP1] = [F:YMP3]
    Affzo [M:YMP1]
  Endif
# YAPMP
Return

##############################################################
$MODIF
# YAPMP
  Gosub MODIF From SUBMWS
  Gosub MODIF From SUBMWS2
  Gosub MODIF From SUBMWS3
  Read [F:YMP0]YMP0 = [M:MWS0]WST;[M:MWS0]WCRFCY
  If !fstat Then
    [F:YMP0] = [M:MWS0]
    [F:YMP0] = [M:YMP1]
    Rewrite [F:YMP0]                                                    # actualización del registro
  Else
    [F:YMP0] = [M:MWS0]
    [F:YMP0] = [M:YMP1]
    Write [F:YMP0]                                                      # creación del registro
  Endif
  GPE=1
# YAPMP
Return

##############################################################
$CREATION
# YAPMP
  Gosub CREATION From SUBMWS
  Gosub CREATION From SUBMWS2
  Gosub CREATION From SUBMWS3
  [F:YMP1] = [M:MWS0]
  [F:YMP1] = [M:YMP1]
  Write [F:YMP1]
  GPE=1
# YAPMP
Return

##############################################################
$ANNULE
# YAPMP
  Gosub ANNULE From SUBMWS
  Gosub ANNULE From SUBMWS2
  Gosub ANNULE From SUBMWS3
  Delete [F:YMP2] Where [F:YMP2]WST = [M:MWS0]WST and [F:YMP2]WCRFCY = [M:MWS0]WCRFCY and [F:YMP2]WCR = [M:MWS0]WCR
  GPE=1
# YAPMP
Return

##############################################################
$SEL_TABLE
  Case TABLE
    When "YSELBOMITM"   : Gosub SEL_YSELBOMITM
  Endcase
Return

##############################################################
$VERF_TABLE
  Case TABLE
    When "YSELBOMITM"   : Gosub VERF_YSELBOMITM
  Endcase
Return

##############################################################
$SEL_YSELBOMITM
# YAPMP
  If !clalev([F:YVB0]) Then : Local File YVISBOMITM [F:YVB0] : Endif
  Default File [YVB0]
  Filter [F:YVB0] Where LANGUE = GLANGUE                                    # filtra sólo las de producción
  NBCOL       = 0                                                           # número de la columna
  TIT(NBCOL)  = "Selección estructuras"                                     # título de la ventana
  NBCOL       += 1
  TIT(NBCOL)  = "Cód. artículo"                                             # título de la segunda columna
  COL(NBCOL)  = "ITMREF"                                                    # nombre del campo a mostrar en la segunda columna
  NBCOL       += 1
  TIT(NBCOL)  = "Descripción"                                               # título de la tercera columna
  COL(NBCOL)  = "TEXTE"                                                     # nombre del campo a mostrar en la tercera columna
# YAPMP
Return

##############################################################
$VERF_YSELBOMITM
Return

##############################################################
$FIN
  Gosub CERRAR_TABLAS
Return

##############################################################
$ABRIR_TABLAS
  If !clalev([F:YMP0]) Then : Local File YMNTOCT    [F:YMP0] : Endif
  If !clalev([F:YMP1]) Then : Local File YMNTOCT    [F:YMP1] : Endif
  If !clalev([F:YMP2]) Then : Local File YMNTOCT    [F:YMP2] : Endif
  If !clalev([F:YMP3]) Then : Local File YMNTOCT    [F:YMP3] : Endif
  If !clalev([F:MFO0]) Then : Local File MFGOPE     [F:MFO0] : Endif
  If !clalev([F:MFH0]) Then : Local File MFGHEAD    [F:MFH0] : Endif
  If !clalev([F:YGC0]) Then : Local File YGAMAC     [F:YGC0] : Endif
Return

##############################################################
$CERRAR_TABLAS
  Close Local File [YMP0],[YMP1],[YMP2],[YMP3],[MFO0],[MFH0],[YGC0]
Return

##############################################################
######################    ETIQUETAS    #######################
##############################################################

##############################################################
#################    ACCIONES DE CAMPO    ####################
##############################################################

######################################################################################
## Después de modificar el campo "Gestión de mnto. preventivo"
######################################################################################
Subprog AM_YGESFLG(VALEUR)
Variable Integer VALEUR
# YAPMP
  If VALEUR = 2 Then
    Actzo [M:YMP1]YUDCRTL, YCANTIDAD, YUNIDAD, YBLKLN
  Else
    Grizo [M:YMP1]YUDCRTL, YCANTIDAD, YUNIDAD, YBLKLN
  Endif
# YAPMP
End

######################################################################################
## Antes de entrada para el campo "Unidades"
######################################################################################
Subprog AS_YUNIDAD(VALEUR)
Variable Integer VALEUR
# YAPMP
  Raz GMENLOC
  Case [M:YMP1]YUDCRTL
    When 1 : GMENLOC(2)=1 : GMENLOC(3)=1 : GMENLOC(4)=1 : GMENLOC(5)=1
    When 2 : GMENLOC(1)=1 : GMENLOC(5)=1
    When 3 : GMENLOC(1)=1 : GMENLOC(2)=1 : GMENLOC(3)=1 : GMENLOC(4)=1
  Endcase
# YAPMP
End

######################################################################################
## Antes de entrada para el campo "YUDUSO"
######################################################################################
Subprog AS_YUDUSO(VALEUR)
Variable Integer VALEUR
  Raz GMENLOC
  GMENLOC(1)=1 : GMENLOC(5)=1
End

######################################################################################
## Click del icono "Mostrar información"
######################################################################################
Subprog CL_YICOINFO(VALEUR)
Variable Char    VALEUR()
# YAPMP
  Call MUESTRA_HISTORICO_OFS([M:MWS0]WST)
# YAPMP

# ******************************
Local Char    CTPREACT(50)
Local Decimal SUMCPLQTY, SUMCPLTIM
Local Date    LASTMFG
  Call GET_WSTSINMNTO([M:YMWS2]YCTPREACTOR,[M:MWS0]WCRFCY,CTPREACT) From SPEYLIB0     # devuelve el CT sin mantenimiento ligado al CT de Preactor
  #Call GET_LASTMFG([M:MWS0]WST,LASTMFG) From SPEYLIB0                                 # devuelve la fecha de la última OF del CT que lanza el proceso
  # La fecha del último mantenimiento la busco directamente de la pantalla de centros de trabajo
  If [M:YMP1]FECHAULTIMOM <> [00/00/0000]
   LASTMFG = [M:YMP1]FECHAULTIMOM
  Else
   LASTMFG = [01/01/1753]
  Endif

  Call GET_CPLQTY(CTPREACT,LASTMFG,SUMCPLQTY,SUMCPLTIM) From SPEYLIB0                 # devuelve cantidad y tiempo desde la última OF del CT
  [M:YMP1]YHORASUSO = SUMCPLTIM
  Case [M:YMP1]YUDUSO
    When 2 : [M:YMP1]YTNUSO = arr((SUMCPLTIM / 24),0.001)                             # tiempo en días
    When 3 : [M:YMP1]YTNUSO = arr(((SUMCPLTIM / 24) / 7),0.001)                       # tiempo en semanas
    When 4 : [M:YMP1]YTNUSO = arr((((SUMCPLTIM / 24) / 7) / 30),0.001)                # tiempo en meses
  Endcase
  [M:YMP1]YCTDUSO   = SUMCPLQTY
  Affzo [M:YMP1]YHORASUSO,YTNUSO,YCTDUSO
# ******************************
End

######################################################################################
## Después de modificar el campo "Unidad de control"
######################################################################################
Subprog AM_YUDCRTL(VALEUR)
Variable Integer VALEUR
# YAPMP
  [M:YMP1]YUNIDAD = 6
  Affzo [M:YMP1]YUNIDAD
# YAPMP
End

######################################################################################
## Después de modificar el campo 'YUDUSO'
######################################################################################
Subprog AM_YUDUSO(VALEUR)
Variable Integer VALEUR

  Case VALEUR
    When 2 : [M:YMP1]YTNUSO = arr(([M:YMP1]YHORASUSO / 24),0.001)
    When 3 : [M:YMP1]YTNUSO = arr((([M:YMP1]YHORASUSO / 24) / 7),0.001)
    When 4 : [M:YMP1]YTNUSO = arr(((([M:YMP1]YHORASUSO / 24) / 7) / 30),0.001)
  Endcase
  Affzo [M:YMP1]YTNUSO

End

######################################################################################
## Después de modificar el campo Gama
######################################################################################
Subprog AM_YGAMA(VALEUR)
Variable Char    VALEUR()
  Read [F:YGC0]YGC0 = VALEUR
  If !fstat Then
    [M:YMP1]YGAMADES = [F:YGC0]DESGAMA
    Affzo [M:YMP1]YGAMADES
  Endif
End

##############################################################
##################    FUNCIONES PROPIAS    ###################
##############################################################
#######################
# YAPMP
#**
#* muestra histórico de OF's
#*
#* @param PWST      > centro de trabajo
#*!
Subprog MUESTRA_HISTORICO_OFS(PWST)
Value     Char    PWST

Local Integer LCON0 : Raz LCON0

  # Saco la línea a la que está asociado el CT de mantenimiento
  Local Char LINE : LINE = [M:YMWS2]YCTPREACTOR    # CT de mantenimiento
  Local Char CT_LINE                  # CT que define la línea asociada al CT de mantenimiento
  If LINE <> ""

  If !clalev([F:YX01]) Then : Local File WORKSTATIO   [F:YX01] : Endif
  If !clalev([F:YX02]) Then : Local File YMNTOCT      [F:YX02] : Endif

  For [YX01] Where [YX01]WCRFCY = [M:MWS0]WCRFCY and [YX01]YCTPREACTOR = LINE
#Infbox num$([YX01]WST)
#Infbox num$([YX01]WCRFCY)
      Read [YX02]YMP0 = [YX01]WST;[YX01]WCRFCY
    If (fstat = 0 and [YX02]YGESFLG <> 2) or fstat <> 0
    CT_LINE = [YX01]WST
    Break
    Endif
  Next
#Infbox num$(CT_LINE)
  Close Local File [YX01],[YX02]
  Endif
  Link [MFO0] With [MFH0]MFG0=[F:MFO0]MFGNUM As [LNK] Where [F:MFO0]EXTWST = CT_LINE and [F:MFH0]STRDAT >= [M:YMP1]FECHAULTIMOM Order By [F:MFH0]STRDAT Asc
  Columns [LNK] ([MFH0]MFGNUM,[MFH0]STRDAT,[MFO0])                      # muestro sólo aquellos campos que me hacen falta
  For [LNK]
    [M:YMP1]MFGNUM(LCON0) = [F:MFH0]MFGNUM
    [M:YMP1]STRDAT(LCON0) = [F:MFH0]STRDAT
    LCON0 += 1
  Next
  [M:YMP1]NBOFM = LCON0
  Affzo [M:YMP1]20

End
# YAPMP

